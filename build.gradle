buildscript {
    ext {
        springBootVersion = '2.3.4.RELEASE'
    }
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    group = 'app.lyd.springbootproject'
    version = '0.0.1'

    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 版本号
    version = '0.0.1'

    // jvm版本要求
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    // 防止因中文字符导致构建失败
    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
    }

    dependencies {
        // spring boot
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-configuration-processor'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        // devtools
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        // test
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        // h2 db
        runtimeOnly 'com.h2database:h2'
        // mybatis
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'
        implementation 'com.github.pagehelper:pagehelper-spring-boot-starter:1.3.0'
        // jwt
        implementation 'io.jsonwebtoken:jjwt:0.9.1'
        // cache
        implementation 'org.springframework.boot:spring-boot-starter-cache'
        implementation 'com.github.ben-manes.caffeine:caffeine:2.8.5'
    }


}